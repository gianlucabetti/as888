000100890918     A*--------------------------------------------------------------*
000200011105     A*  DS PER PASSAGGIO PARAMENTRI TASSAZIONE BOLLA PADRONCINI
000300890918     A*--------------------------------------------------------------*
000400931007     A                                      REF(CNDIZION)
000500020131      *
000600020131     A          R FICNB2                    TEXT('TASSAZIONE BOLLA PADRONCINI')
000700020205      *
000800020205     A            §TAPGM        10A         TEXT('PGM CHIAMANTE')
000900020213     A            §TATSM         1A         TEXT('LIVELLO DI CHIAMATA')
001000020131      *
001100020326     A            §TAFGS         3S 0       TEXT('P.O. DI GESTIONE')
001200931007     A            §TAPDR         7S 0       TEXT('COD.PADRONCINO')
001300020212     A****        §TAPDU         7S 0       TEXT('COD.PADR.UNIF.')
001400020131      *
001500950117     A            §TATSR    R               REFFLD($TSR)
001600931022     A            §TASML    R               REFFLD($SML)
001700020131     A            §TACTR         3S 0       TEXT('CODICE TARIFFA')
001800020131      *
001900020131      * RIFERIMENTI NR.DOC. PER DETTAGLIO CONTEGGI C.E.
002000020131     A            §TANDC         7S 0       TEXT('NR.DOCUMENTO')
002100950119     A            §TADDC         8S 0       TEXT('DATA DOCUMENTO')
002200020131      *
002300020131      * RIFERIMENTI BOLLA PER DETTAGLIO CONTEGGI C.E.
002400020131     A            §TAAAS         4S 0       TEXT('ANNO')
002500020131     A            §TALNP         3S 0       TEXT('LINEA PARTENZA')
002600020131     A            §TANRS         2S 0       TEXT('SERIE')
002700020131     A            §TANSP         7S 0       TEXT('SPEDIZIONE')
002800030320     A            §TACBO         2A         TEXT('COD.BOLLA')
002900020131     A*
003000020326     A            §TAKSC         7S 0       TEXT('MITTENTE RITIRI')
003100020326     A*
003200020201      *------------------------------------------------------*
003300020201      * USATI SOLO NELLA CONFERMA CONTEGGI  (SNP/SNT/SNE)
003400020201      *------------------------------------------------------*
003500950117     A            §TASNP    R               REFFLD($SNP)
003600950117     A            §TASNT    R               REFFLD($SNT)
003700950117     A            §TASNE    R               REFFLD($SNE)
003800020201      *------------------------------------------------------*
003900020201     A            §TASPP         7S 0       TEXT('FLG" "=TOTALE STOP')
004000020201     A*
004100020201     A* §TASET A LIVELLO DI DETTAGLIO: tot.sped.ritirate/consegnate nel
004200020201     A* record dettaglio tassazione da applicare con la tariffa a
004300020201     A* spedizione
004400020201     A* §TASET A LIVELLO DI PRESTAZIONE: tot.spedizioni in distinta per
004500020201     A* l'applicazione del bonus per la prestazione consegna
004600020201     A            §TASET    R               REFFLD($SET)
004700030404     A*
004800030404     A            §TASCO    R               REFFLD($SET)
004900030404     A                                      TEXT('TOT.COLLI CONSEGNATI')
005000020131     A            §TANCL         7S 0       TEXT('TOT.COLLI SPEDIZ. DA PAGARE')
005100030821      * Nello stesso Stop totale nr.di Spedizioni
005200030821     A            §TANSS         5S 0       TEXT('NR.SPEDIZIONI X STOP')
005300030821     A            §TAFW          5S 0       TEXT('NR.SPED.X STOP TIPO "FW"')
005400030821     A            §TARCC         5S 0       TEXT('NR.SPED.X STOP TIPO "RCC"')
005500021212      *
005600021217     A            §TATBS         5S 0       TEXT('NR.RITIRI ANNULLATI')
005700950117     A* PER PIKING: tot.colli spedizioni da pagare e non pagare
005800950117     A* PER ETICHETTATURA: tot. colli per etichettatura da FTDNCE
005900021212     A            §TADKC         1          TEXT('S=ABILITATO A DISK C -> PER -
006000021212     A                                      TARIFFA <ETC> INVECE CHE <ETI>')
006100950117     A            §TACPE         7S 0       TEXT('TOT.COLLI PER PIK/ETICHETTAT')
006200950117     A            §TAPPE         1          TEXT('S=PAGAMENTO PIK/ETICH.')
006300020506     A            §TAPGE         1          TEXT('S=PAGAMENTO GIORNATA/ETIC.')
006400020506     A            §TAPGP         1          TEXT('S=PAGAMENTO GIORNATA/PICK.')
006500021211      *
006600021211     A            §TAFCS         1          TEXT('S=PAGAMENTO CAR/SCARICO')
006700021216      *
006800020131     A            §TAKFA         8S 1       TEXT('TOTALE PESO')
006900020131     A            §TATVL         9S 3       TEXT('TOTALE VOLUME')
007000020131      *
007100020201      *------------------------------------------------------*
007200020201      * USATI SOLO IN TASSAZIONE
007300020201      *    (CAS/CA1/VCA/IFP/TSP/TC1/TC2/FIN/FGC/GVA/SIC)
007400020201      *------------------------------------------------------*
007500950131     A* §TACA1 = IMPORTO DEL CONTRASSEGNO SU CUI CALCOLARE
007600950131     A*          TARIFFA PARTICOLARE SU CONTRASS.PRONTAMENTE RECUPERATI
007700020131     A            §TACAS        13S 3       TEXT('C/ASSEGNO')
007800950131     A            §TACA1        13S 3       TEXT('C/ASSEGNO')
007900020131      *
008000950116     A            §TAVCA    R               REFFLD($VCA)
008100011106     A            §TAIFP        13S 3       TEXT('IMPORTO FATTURA ASSEGNATO +
008200950117     A                                      INCASSATO')
008300931007     A            §TATSP    R               REFFLD($TSP)
008400950116     A            §TATC1    R               REFFLD($TC1)
008500950116     A            §TATC2    R               REFFLD($TC1)
008600950116     A                                      TEXT('2 CONSEGNA PARTICOLARE')
008700020325     A            §TAXPR         1A         TEXT('X CALCOLO ESPRESSO')
008800020325     A            §TAISO         1A         TEXT('X CALCOLO STOP A ISOLA')
008900030109     A            §TADED         1A         TEXT('X STOP DEST.DISAGIATO')
009000931022     A            §TAFIN         1A         TEXT('INOLTRO SE "C" ANTEPORTO +
009100950116     A                                      SE "R" +
009200950116     A                                      "C"=CITTA +
009300950116     A                                      "P"=PROVINCIA +
009400950116     A                                      "I"=ISOLA')
009500931022     A            §TAFGC         1A         TEXT('S=RICONSEGNA GIACENZA')
009600950117     A            §TAGVA    R               REFFLD($GVA)
009700950117     A            §TAPAG         1          TEXT('S=SPEDIZ.PAGATA')
009800950117     A            §TASIC    R               REFFLD($SIC)
009900020201      *------------------------------------------------------*
010000020131     A            §TAFGR         1A         TEXT('FLAG GIORNATA:  +
010100020131     A                                      " "=NIENTE +
010200020131     A                                      "M"=MEZZA +
010300020131     A                                      "I"=INTERA')
010400020131     A            §TAFLG         1A         TEXT('" "=TASSAZIONE CON DETTAGLIO +
010500020131     A                                      TARIFFARIO +
010600020131     A                                      "S"=TASSAZIONE PRESTAZIONE +
010700020131     A                                      "C"=CHIUSURA FILES')
010800020131     A            §TADIV         3          TEXT('DIVISA')
010900020201     A            §TACAP         5A         TEXT('CAP DESTINATARIO SE "C" +
011000020201     A                                      MITTENTE SE "R"')
011100020327      *------------------------------------------------------*
011200020327      * PER DIVIDERE A GIORNATA LE VOCI DI C/E
011300020327      *------------------------------------------------------*
011400020328     A            §TASTP         5S 0       TEXT('NR.STOP')
011500020327     A            §TAKGC         8S 1       TEXT('KG.CONSEGNE')
011600020327     A            §TAKGR         8S 1       TEXT('KG.RITIRI  ')
011700020201      *------------------------------------------------------*
011800020201     A*  CAMPI DI OUTPUT
011900020201      *------------------------------------------------------*
012000020131     A            §TAITT        10S 3       TEXT('OUT:TASSAZIONE DA TAR.BASE')
012100020131     A            §TAITA        10S 3       TEXT('OUT:TASSAZIONE DA ACCESS.')
012200020131     A            §TATST        10S 3       TEXT('OUT:TARIFFA STOP')
012300020131     A            §TATPE        10S 3       TEXT('OUT:TARIFFA PIK/ETI.')
012400020131     A            §TATBN        10S 3       TEXT('OUT:TARIFFA BONUS')
012500020131     A            §TAMNT        10S 3       TEXT('OUT:MINIMO GIORNATA')
012600020131     A            §TATIG        10S 3       TEXT('OUT:TARIFFA GIORNATA')
012700020131     A            §TAIAD        10S 3       TEXT('OUT:IMP.ADDIZIONALE -
012800020131     A                                      PER TAR.A PRESTAZIONE')
012900020506     A            §TATGP        10S 3       TEXT('OUT:TARIFFA GIORNATA PCK')
013000020506     A            §TATGE        10S 3       TEXT('OUT:TARIFFA GIORNATA ETI')
013100021218     A            §TARAN        10S 3       TEXT('OUT:TARIFFA RITIRI ANNULLATI')
013200020506      *------------------------------------------------------*
013300020131     A            §TAERR         1A         TEXT('OUT:FLAG ERRORE')
013400040506     A            §TATER         1A         TEXT('OUT:TIPO ERRORE')
013500090507      *------------------------------------------------------*
013600090521      *  AGGIUNTI DA NUOVE TARIFFE SUPERTESTATA
013700090507     A            §TASOC         3          TEXT('OUT:SOCIETA    ')
013800090507     A            §TACDF         7S 0       TEXT('OUT:COD.FORNITORE')
013900090521     A            §TAPIVA       16          TEXT('OUT:P.IVA      ')
014000090908      *--------------------------------------------------------------------*
014100090908      *  PARTICOLARITA' SULLE SPEDIZIONE DEL CLIENTE DA PAGARE
014200090908      *  DIVERSAMENTE. LO STOP PUÒ ESSERE PAGATO SOLO SE TUTTE LE BOLLE
014300090908      *  GLI APPARTENGONO COSÌ COME LE ALTRE TARIFFE CHE RIGUARDANO
014400090908      *  PIÙ BOLLE-> LA TARIFFA PUÒ ESSERE APPLICATA SOLO SE TUTTE LE
014500090908      *  BOLLE SONO SUE ALTRIMENTI SI DEVE APPLICARE QUELLA STANDARD.
014600090908      *--------------------------------------------------------------------*
014700090914     A            §TACLIPAR      7S 0       TEXT('SE >0 CI SONO DELLE TARIFFE-
014800090914     A                                       PARTICOLARI SUL CLIENTE DA APPLI-
014900090914     A                                      CARE. SE =9999999 CI SONO PIÙ CLI-
015000090914     A                                      ENTI SULLO STOP CON TAR.PARTICOLA-
015100090914     A                                      RI')
015200090911     A            §TATUTTE       1A         TEXT('S/N TUTTE BOLLE CON TAR.PAR-
015300090911     A                                      TICOLARE X GESTIRE LO STOP')
015400090908      *--------------------------------------------------------------------*
