000100971021     A*--------------------------------------------------------------*
000200971021     A*  DS PER PGM DI MANUTENZIONE DESCRIZIONI
000300971021     A*--------------------------------------------------------------*
000400971021     A                                      REF(CNDIZION)
000500971021     A          R D10REC                    TEXT('DS PER MANUTENZIONE DESCRIZI-
000600971021     A                                      ONI')
000700971021     A*********************************
000800971021     A* C A M P  I   D I    I N P U T
000900971021     A*********************************
001000971021     A            I10TLA         1A         TEXT('TIPO LANCIO PROGRAMMA')
001100971021     A*
001200971021     A* *LASCIARE VUOTO QUESTO CAMPO SE IL CHIAMANTE DEMANDA A QUESTO
001300971021     A*  PGM L'INTERA GESTIONE DELLE DESCRIZIONI (MANUTENZIONE SUBFILE
001400971021     A*  + CONFERMA DATI SU DATA BASE)
001500980526     A*
001600980526     A* *METTERE "V" SE IL CHIAMANTE DEMANDA A QUESTO PGM L'INTERA
001700980526     A*  GESTIONE DELLE DESCRIZIONI (MANUTENZIONE+CONFERMA DATI)
001800980526     A*  MA E' NECESSARIO LO SCAMBIO DEL CAMPO I10NOT/O10NOT DA CHIAMAN
001900980526     A*  TE A CHIAMATO E VICEVERSA (UTILIZZATO IN PARTICOLARE IN
002000980526     A*  CONFERMA C.A.)
002100971029     A*
002200971028     A* *METTERE "M" PER EFFETTUARE LA SOLA IMMISSIONE DATI NEL SUBFILE
002300971029     A*  Serve se in fase di immissione la key intera del file descriz.
002400971029     A*  non è ancora disponibile e non è quindi possibile confermare i
002500971029     A*  dati. (Per un esempio di questo utilizzo vedere il pgm di
002600971029     A*  apertura C.A.)
002700971029     a*
002800971021     A* *METTERE "C" PER EFFETTUARE L'AGGIORNAMENTO DEL FILE DI DATA
002900971021     A*  BASE IN BASE AI DATI PRESENTI NEL SUBFILE
003000971029     A*  Il richiamo con "C" serve per confermare i dati precedentemen-
003100971029     A*  te immessi in modalità "M".
003200971029     A*  N.B.:E' possibile effettuare più di un richiamo consecutivo in
003300971029     A*  modalità "M". L'importante è che l'ultimo richiamo con "M"
003400971029     A*  sia sempre seguito da un richiamo con "C" per non perdere
003500971029     A*  i dati inseriti fino a quel momento
003600971029     A*
003700971028     A* *METTERE "I" PER EFFETTUARE LA SOLA INTERROGAZIONE DEI DATI
003800971021     A            I10FLM         1A         TEXT('FLAG MODALITA'':" "=ELABORAZ-
003900971028     A                                      .COMPLETA;"M"=SOLO IMMISS.;"C"=CONF-
004000980716     A                                      ERMA DATI;"I"=INTERROG.;"V"=VARIAZ;-
004100980716     A                                      "R"=RECUPERO;"W"=SCRITTURA')
004200971021     A******
004300971021     A* TPD,NKT,STD,NKS,TRC ---> E' LA CHIAVE DEL FILE DESCRIZIONI
004400971021     A******
004500971029     A* TPD --> NON IMMETTERE SE D10FLM="C"
004600971029     A*         OBBLIGATORIO IN TUTTI GLI ALTRI CASI
004700971021     A            I10TPD    R               REFFLD($TPD)
004800971029     A* NKT --> NON IMMETTERE SE D10FLM="M"
004900971029     A*         OBBLIGATORIO IN TUTTI GLI ALTRI CASI
005000971021     A            I10NKT    R               REFFLD($NK3)
005100971029     A* STD --> NON IMMETTERE SE D10FLM="C"
005200971029     A*         OBBLIGATORIO, SE PREVISTO, IN TUTTI GLI ALTRI CASI
005300971021     A            I10STD    R               REFFLD($STD)
005400971029     A* NKS --> NON IMMETTERE SE D10FLM="C"
005500971029     A*         OBBLIGATORIO, SE PREVISTO, IN TUTTI GLI ALTRI CASI
005600980527     A            I10NKS    R               REFFLD($NK4)
005700971029     A* TRC --> NON IMMETTERE SE D10FLM="C"
005800971029     A*         OBBLIGATORIO IN TUTTI GLI ALTRI CASI
005900971021     A            I10TRC    R               REFFLD($TRC)
006000980608     A* DIM --> IMMETTERE NEL CASO DI GESTIONE NOTE
006100980608     A*         DATA IMMISSIONE FASE
006200980608     A            I10DIM    R               REFFLD($DIM)
006300980608     A* HIM --> IMMETTERE NEL CASO DI GESTIONE NOTE
006400980608     A*         ORA  IMMISSIONE FASE
006500980608     A            I10HIM    R               REFFLD($HIM)
006600980526     A* SOLO SE I10FLM="M" O "V" --> IMMETTERE LE NOTE CHE SI VOGLIONO
006700971030     A* VISUALIZZARE NELLA PRIMA RIGA DEL SUBFILE
006800971030     A* CAMPO FACOLTATIVO
006900971030     A            I10NOT    R               REFFLD($NOT)
007000980716     A* DATA DELLA C.A. O DELL'EVENTO DA ESPORRE IN TESTATA IN FORMATO GGMMAAAA
007100980716     A            I10DTA    R               REFFLD($DA8)
007200980608     A*
007300971021     A***********************************
007400971021     A* C A M P I    D I   O  U T P U T
007500971021     A***********************************
007600971021     A* O10NOT CONTIENE LA PRIMA RIGA DI DESCRIZIONE
007700971021     A            O10NOT    R               REFFLD($NOT)
007800971024     A            O10F03         1A         TEXT('"S"=PREMUTO F3')
007900971024     A            O10F12         1A         TEXT('"S"=PREMUTO F12')
008000971021     A* SEMPRE QUANDO SI PRESENTA UN ERRORE
008100971021     A            O10ERR         1A         TEXT('CODICE DI ERRORE-CORRISPONDE -
008200971021     A                                      ALLA POSIZ. DEL MSG IN SKIERA')
008300971021     A            O10MSG        78A         TEXT('MESSAGGIO DI ERRORE')
