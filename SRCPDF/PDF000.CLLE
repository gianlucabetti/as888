000100000000PGM (&KLIST &ELENCO &PRINTER &FORMTYPE &DRAWER)
000200000000
000300000000  Dcl &KLIST      *Char 15
000400000000  Dcl &ELENCO     *Char 30
000500000000  Dcl &PRINTER    *Char 10
000600000000  Dcl &FORMTYPE   *Char 10
000700000000  Dcl &DRAWER     *Char 10
000800000000
000900000000  DclF PDFTABEL  RTABEL  OPNID(TABEL)
001000000000  DclF PDFLIST   RPDF    OPNID(ELENCO)
001100000000
001200000000  Dcl &LIBRNAME   *Char 10
001300000000  Dcl &FILENAME   *Char 10
001400000000  Dcl &MBRNAME    *Char 10
001500000000
001600130226  Dcl &DocPath    *Char 128
001700130226  Dcl &WorkPath   *Char 128
001800130226  Dcl &JobPath    *Char 128
001900130226  Dcl &PdfPath    *Char 128
002000000000  Dcl &CopieDDT   *Char 1
002100000000  Dcl &Server     *Char 1
002200000000  Dcl &JServer    *Char 4
002300000000  Dcl &MaxPDF     *Dec  5
002400000000  Dcl &MaxSizeKB  *Dec  9
002500000000  Dcl &PdfSize    *Dec  9
002600000000  Dcl &CurSize    *Dec  9
002700000000  Dcl &TotSize    *Dec  9
002800000000  Dcl &NumCopia   *Dec  3
002900000000
003000130226  Dcl &TstPath    *Char  128
003100000000  Dcl &Null       *Char  1    VALUE(X'00')
003200000000  Dcl &Struct     *Char  128
003300000000  Dcl &Rc         *Int   4
003400000000  Dcl &Mode       *Int   4  /* API define F_OK = 0 (integer) */
003500000000
003600000000  Dcl &FileInfo   *Char 128
003700000000  Dcl &ObjSize    *UInt        Stg(*Defined) DefVar(&FileInfo 21)
003800000000  Dcl &ObjAlloc   *UInt        Stg(*Defined) DefVar(&FileInfo 45)
003900000000  Dcl &ObjTyp     *Char 10     Stg(*Defined) DefVar(&FileInfo 49)
004000000000
004100130227  Dcl &DTAInfo    *Char  140
004200000000  Dcl &InPDF      *Char  22000
004300000000  Dcl &SavPDF     *Char  22000
004400000000  Dcl &InDTA      *Char  22000
004500000000  Dcl &OutPDF     *Char  63
004600000000  Dcl &CurPdf     *Dec   5
004700000000  Dcl &BlkPdf     *Dec   5
004800000000  Dcl &Debug      *Char  1
004900000000  Dcl &JError     *Lgl
005000000000  Dcl &Msg        *Char  100
005100000000  Dcl &Cmd        *Char  100
005200000000
005300000000  Dcl &oDebug     *Char  10
005400000000
005500130226  Dcl &OutPCL     *Char  64
005600000000  Dcl &Device     *Char  20
005700000000  Dcl &Resolution *Dec   4
005800000000
005900000000  Dcl &SplfName   *Char  10
006000000000  Dcl &JobNumber  *Char  6
006100000000  Dcl &UserName   *Char  10
006200000000  Dcl &JobName    *Char  10
006300000000  Dcl &SplfNumber *Char  6
006400000000
006500000000  Dcl &OFF        *dec   5
006600000000  Dcl &Error      *Lgl
006700000000  Dcl &Retry      *dec   3
006800000000  Dcl &aRetry     *Char  3
006900000000  Dcl &FlgMsg     *Lgl
007000000000  Dcl &FlgLog     *Lgl
007100000000
007200000000  Dcl &JobNbr     *Char 6
007300000000  Dcl &SpcINPDF   *Char 20
007400000000  Dcl &SpcOUPDF   *Char 20
007500000000  Dcl &SpcINDTA   *Char 20
007600000000  Dcl &SpcCOPDF   *Char 20
007700000000  Dcl &INPDFOff   *Int  4
007800000000  Dcl &INPDFLen   *Int  4
007900000000  Dcl &INDTAOff   *Int  4
008000000000  Dcl &INDTALen   *Int  4
008100000000  Dcl &OUPDFOff   *Int  4
008200000000  Dcl &OUPDFLen   *Int  4
008300000000  Dcl &SpcOff     *Int  4
008400000000  Dcl &SpcLen     *Int  4
008500000000  Dcl &SpcSize    *Int  4
008600000000  Dcl &CR         *Char 1    Value(x'25')
008700000000  Dcl &CpyFromPDF *Char 63
008800000000  Dcl &CpyToPDF   *Char 63
008900000000  Dcl &CpyFromDTA *Char 63
009000000000  Dcl &CpyToDTA   *Char 63
009100000000
009200000000  Dcl &X          *Int  2
009300000000  Dcl &EOF        *Lgl
009400000000
009500000000  Dcl &Length     *Int  4
009600000000  Dcl &ErrLength  *Int  4
009700000000  Dcl &ErrMsg     *Char 256
009800000000  Dcl &ErrMsgL    *Int  4
009900000000  Dcl &Type       *Char 1
010000000000  Dcl &SbmJob     *Char 10
010100000000  Dcl &JOBI0400   *Char 1562
010200000000
010300000000  /* Variabili per LOG  */
010400000000  Dcl &LogKey     *Char 10
010500000000  Dcl &LogSev     *Char 2
010600140828  Dcl &LogData    *Char 128
010700000000  Dcl &LogMsg     *Char 40
010800000000  Dcl &TotPDF     *Dec  6
010900000000  Dcl &TotNoPDF   *Dec  6
011000000000  Dcl &Sql        *Char 200
011100130308
011200000000  /* Variabili per Stampa Quality */
011300000000  Dcl &pHiRes     *Char  1
011400000000  Dcl &cHiRes     *Char  1
011500000000  Dcl &HiRes      *Dec   4
011600000000  Dcl &LoRes      *Dec   4
011700000000
011800000000  /* Generico errori */
011900000000  MonMsg (CPF0000 MCH0000) Exec(Goto ERROR)
012000000000
012100000000  /*?Imposta i parametri elaborazione PDF  */
012200000000  RclRsc
012300000000  ChgVar &EOF '0'
012400000000  OVRDBF PDFTABEL SHARE(*YES)
012500000000  CLOF TABEL
012600000000  MONMSG CPF4520
012700000000  OPNQRYF FILE((PDFTABEL *FIRST RTABEL)) OPNID(TABEL) +
012800000000          QRYSLT('TBKEY = "DDT"')
012900000000  RCVF OPNID(TABEL)
013000000000  MONMSG CPF0864 EXEC(ChgVar &EOF '1')
013100000000  DoWhile (*NOT &EOF)
013200000000    Select
013300000000      When (&TABEL_TBELE = 'DOCPATH') +
013400000000        ChgVar &DocPath &TABEL_TBDAT
013500000000      When (&TABEL_TBELE = 'WORKPATH') +
013600000000        ChgVar &WorkPath &TABEL_TBDAT
013700000000      When (&TABEL_TBELE = 'PCLDEVICE') +
013800000000        ChgVar &Device &TABEL_TBDAT
013900000000      When (&TABEL_TBELE = 'PCLRESOLUTION') +
014000000000        ChgVar &LoRes %sst(&TABEL_TBDAT 1 4)
014100000000      When (&TABEL_TBELE = 'PCLRESOLUTIONH') +
014200000000        ChgVar &HiRes %sst(&TABEL_TBDAT 1 4)
014300000000      When (&TABEL_TBELE = 'SERVER') +
014400000000        ChgVar &Server &TABEL_TBDAT
014500000000      When (&TABEL_TBELE = 'DEBUG') +
014600000000        ChgVar &Debug &TABEL_TBDAT
014700000000      When (&TABEL_TBELE = 'MAXPDFSIZE') +
014800000000        ChgVar &MaxSizeKB %sst(&TABEL_TBDAT 1 9)
014900000000      When (&TABEL_TBELE = 'MAXPDFNUM') +
015000000000        ChgVar &MaxPdf %sst(&TABEL_TBDAT 1 5)
015100000000      When (&TABEL_TBELE = 'COPIEDDT') +
015200000000        ChgVar &CopieDdT %sst(&TABEL_TBDAT 1 1)
015300000000    EndSelect
015400000000    RCVF OPNID(TABEL)
015500000000    MONMSG CPF0864 EXEC(ChgVar &EOF '1')
015600000000  EndDo
015700000000  CLOF TABEL
015800000000  DLTOVR PDFTABEL
015900000000
016000000000  /*?Normalizza workpath */
016100000000  ChgVar &X 63
016200000000  DoWhile (&X > 1)
016300000000    If (%sst(&WorkPath &X 1) *NE ' ') Do
016400000000      If (%sst(&WorkPath &X 1) *NE '/') Do
016500000000        ChgVar &X (&X + 1)
016600000000        ChgVar %sst(&WorkPath &X 1) '/'
016700000000      EndDo
016800000000    Leave
016900000000    EndDo
017000000000    ChgVar &X (&X - 1)
017100000000  EndDo
017200000000
017300000000  /*?Reperisce il numero del lavoro */
017400000000  RtvJoba Nbr(&JobNbr)
017500000000
017600000000  /*?Imposta aggiornamento elenco PDF */
017700000000  ChgVar &Sql +
017800000000    'UPDATE PDFLIST SET PDFPRTFST = " ", +
017900000000                        PDFPRTDT = current date, +
018000000000                        PDFPRTTM = current time +
018100000000     WHERE PDFKLIST = " ...+... 1 ...+" +
018200130226       AND PDFKEY = "*...+....1....+....2....+....3....+"'
018300000000
018400000000  /*?Crea un path specifico per questa richiesta */
018500000000  ChgVar &JobPath (&WorkPath *TCAT &JobNbr *CAT '/')
018600000000  MD &JobPath DTAAUT(*RWX) OBJAUT(*ALL)
018700000000  MonMsg CPF0000
018800000000
018900130308  /*?Crea 2 user-spaces per elenco Pdf (INPDF) e dati aggiuntivi (INDTA) */
019000000000  ChgVar &SpcINPDF ('I' *CAT &JobNbr *TCAT 'PDFQTEMP')
019100000000  Call QUSCRTUS +
019200000000    (&SpcINPDF ' ' x'00100000' x'40' *ALL +
019300000000     'PDF per ADDINFO' *yes X'00000000')
019400000000  ChgVar &SpcINDTA ('I' *cat &JobNbr *TCAT 'DTAQTEMP')
019500000000  Call QUSCRTUS +
019600000000    (&SpcINDTA ' ' x'00100000' x'40' *ALL +
019700000000     'PDF per ADDINFO' *yes X'00000000')
019800000000
019900000000  ChgVar &SpcOUPDF ('O' *CAT &JobNbr *TCAT 'PDFQTEMP')
020000000000
020100000000  /*?estrae le bolle */
020200000000  CLOF ELENCO
020300000000  MONMSG CPF4520
020400000000  ChgVar &FILENAME %sst(&ELENCO 1 10)
020500000000  ChgVar &LIBRNAME %sst(&ELENCO 11 10)
020600000000  ChgVar &MBRNAME  %sst(&ELENCO 21 10)
020700000000  If (&LIBRNAME = ' ') ChgVar &LIBRNAME *LIBL
020800000000  If (&MBRNAME = ' ' ) ChgVar &MBRNAME  *FIRST
020900000000  OVRDBF PDFLIST &LIBRNAME/&FILENAME &MBRNAME SHARE(*YES)
021000000000  OPNQRYF  FILE((PDFLIST)) +
021100000000           QRYSLT('PDFKLIST *EQ "' *CAT &KLIST *TCAT '"') +
021200000000           OPNID(ELENCO)
021300000000
021400000000  /*?legge i Pdf da stampare */
021500000000  ChgVar &INPDFOff 1
021600000000  ChgVar &INPDFLen 63
021700000000  ChgVar &INDTAOff 1
021800000000  ChgVar &INDTALen 140
021900000000  ChgVar &EOF '0'
022000000000  RCVF OPNID(ELENCO)
022100000000  MONMSG CPF0864 EXEC(ChgVar &EOF '1')
022200000000
022300000000  DoWhile (*NOT &EOF)
022400000000    If (&ELENCO_PDFPATH *NE ' ') +
022500000000      ChgVar &PdfPath (&ELENCO_PDFPATH *TCAT '/' *CAT +
022600000000                       &ELENCO_PDFKEY *TCAT '.pdf')
022700000000    Else +
022800000000      ChgVar &PdfPath (&DocPath *TCAT '/' *CAT +
022900000000                       &ELENCO_PDFKEY *TCAT '.pdf')
023000130308
023100000000    /*?Verifica l'esistenza del PDF */
023200130227    CHKPDF:
023300000000    ChgVar &TstPath (&PdfPath *TCAT &Null)
023400000000    CALLPRC PRC('access') PARM((&TstPath) (&Mode *BYVAL)) +
023500000000                          RTNVAL(&Rc)
023600000000    /*?Se esiste ....  */
023700000000    If (&Rc *EQ 0) Do
023800000000      ChgVar &TotPDF (&TotPDF + 1)
023900000000      /*?... log */
024000000000      ChgVar &LogData &ELENCO_PDFKEY
024100000000      ChgVar &LogMsg ('Elenco' *BCAT &KLIST)
024200000000      Call PDFLOGR ('PDF' &LogData '00' &LogMsg)
024300000000      MonMsg CPF0000
024400000000      /*?... lo inserisce nella lista INPDF */
024500000000      ChgVar %sst(&PdfPath 63 1) &CR
024600000000      Call QUSCHGUS (&SpcINPDF &INPDFOff &INPDFLen &PdfPath '0')
024700000000      ChgVar &PdfPath (&JobPath *TCAT +
024800000000                       &ELENCO_PDFKEY *TCAT '.pdf')
024900000000      /*?. inserisce le info aggiuntive nella lista INDTA  */
025000130227      /* 001-120 - Info aggiuntive                                     */
025100130227      /* 132-132 - H=Alta Risoluzione                                  */
025200130227      /* 133-133 - Copie Mittente                                      */
025300130227      /* 134-134 - Copie Destinatario                                  */
025400130227      /* 135-135 - Copie Servizio                                      */
025500130227      /* 136-136 - NO INFO                                             */
025600130227      /* 140-140 - /=CR                                                */
025700000000      /*---------------------------------------------------------------*/
025800000000      ChgVar &DtaInfo ' '
025900000000      /*?... Info */
026000000000      If (&ELENCO_PDFINFO =' ') ChgVar &DtaInfo &ELENCO_PDFKEY
026100000000      Else ChgVar &DtaInfo &ELENCO_PDFINFO
026200000000      /*?... Risoluzione */
026300130227      If (&ELENCO_PDFRESOL = ' ') ChgVar %sst(&DtaInfo 132 1) 'S'
026400000000      Else ChgVar %sst(&DtaInfo 132 1) &ELENCO_PDFRESOL
026500000000      /*?... Numero Copie */
026600130227      If (&ELENCO_PDFCOPIEM < '0') ChgVar %sst(&DtaInfo 133 1) '0'
026700000000      Else ChgVar %sst(&DtaInfo 133 1) &ELENCO_PDFCOPIEM
026800130227      If (&ELENCO_PDFCOPIED < '0') ChgVar %sst(&DtaInfo 134 1) '1'
026900000000      Else ChgVar %sst(&DtaInfo 134 1) &ELENCO_PDFCOPIED
027000130227      If (&ELENCO_PDFCOPIES < '0') ChgVar %sst(&DtaInfo 135 1) '0'
027100000000      Else ChgVar %sst(&DtaInfo 135 1) &ELENCO_PDFCOPIES
027200000000      /*?... Inserisce il Carriage Return */
027300130227      ChgVar %sst(&DtaInfo 140 1) &CR
027400000000      Call QUSCHGUS (&SpcINDTA &INDTAOff &INDTALen &DtaInfo '0')
027500000000      /*?... incrementa la posizione nelle liste */
027600000000      ChgVar &INPDFOff (&INPDFOff + &INPDFLen)
027700000000      ChgVar &INDTAOff (&INDTAOff + &INDTALen)
027800000000
027900000000      /*?... aggiorna l'elenco */
028000000000      ChgVar %sst(&Sql 33 1) S
028100000000      ChgVar %sst(&Sql 104 15) &KLIST
028200130226      ChgVar %sst(&Sql 135 35) &ELENCO_PDFKEY
028300000000      XSQL SQLSTMT(&SQL) LOGSQL(*NO)
028400000000    EndDo
028500000000
028600000000    /*?Se non esiste .... */
028700000000    Else Do
028800130308      /**if not già riprovato *************************************************/
028900130227      /**PDF002    PATH(&.........................) +                         */
029000130227      /**          PDF(&ELENCO_PDFKEY *TCAT '.pdf') +                         */
029100130227      /**          RESOL(&PRESOL)                   +                         */
029200130227      /**          ORIENT(*AUTO)                    +                         */
029300130227      /**          PDFDEL(*YES)                                               */
029400130227      /**goto chkpdf          *************************************************/
029500130227      /***************************** PROVARE PDF002 ***************************/
029600130308
029700130308
029800130227      /************************************************************************/
029900000000      ChgVar &TotNoPDF (&TotNoPDF + 1)
030000000000      /*?... log */
030100000000      ChgVar &LogData &ELENCO_PDFKEY
030200000000      ChgVar &LogMsg ('PDF mancante')
030300000000      Call PDFLOGR ('PDF' &LogData '10' &LogMsg)
030400000000      MonMsg CPF0000
030500000000      /*?... aggiorna l'elenco */
030600000000      ChgVar %sst(&Sql 33 1) E
030700000000      ChgVar %sst(&Sql 104 15) &KLIST
030800130226      ChgVar %sst(&Sql 135 35) &ELENCO_PDFKEY
030900000000      XSQL SQLSTMT(&SQL) LOGSQL(*NO)
031000000000    EndDo
031100000000    /*?legge il PDF successivo */
031200000000    ChgVar &EOF '0'
031300000000    RCVF OPNID(ELENCO)
031400000000    MONMSG CPF0864 EXEC(ChgVar &EOF '1')
031500000000  EndDo
031600000000  CLOF ELENCO
031700000000  DLTOVR PDFLIST
031800000000
031900000000  /*?Se non ci sono PDF, termina */
032000000000  if (&TotPdf *EQ 0) Do
032100000000    If (&TotNoPdf > 0) Do
032200000000      ChgVar &LogData &KLIST
032300000000      ChgVar &LogMsg  ('Elenco senza Pdf')
032400000000      Call PDFLOGR ('ELENCO' &LogData '10' &LogMsg +
032500000000                    &TotPdf &TotNoPdf *CLOSE)
032600000000      MonMsg CPF0000
032700000000    EndDo
032800000000    If (&Debug *NE S) Do
032900000000      RD &JobPath
033000000000      MonMsg CPFA0AC Exec(Do)
033100000000        chgvar &Cmd ('rm -rf' *BCAT &JobPath)
033200000000        QSH CMD(&Cmd)
033300000000        MonMsg Cpf0000
033400000000      EndDo
033500000000    EndDo
033600000000    Goto FINE
033700000000  EndDo
033800000000
033900000000  /*?Inserisce un elemento vuoto in elenco  */
034000000000  ChgVar &PdfPath ' '
034100000000  ChgVar %sst(&PdfPath 63 1) &CR
034200000000  Call QUSCHGUS (&SpcINPDF &INPDFOff &INPDFLen &PdfPath '0')
034300000000
034400000000  /*?Salva l'elenco dei PDF (INPDF) su IFS */
034500000000  ChgVar &CpyFromPDF ('/QSYS.LIB/QTEMP.LIB/' *CAT +
034600000000                      %sst(&SpcINPDF 1 10) *CAT '.USRSPC')
034700000000  ChgVar &CpyToPDF (&JobPath *TCAT %sst(&SpcINPDF 1 10))
034800000000  CPY OBJ(&CpyFromPDF) +
034900000000      TOOBJ(&CpyToPDF) +
035000000000      TOCCSID(819) DTAFMT(*TEXT) REPLACE(*YES)
035100000000
035200000000  /*?Salva l'elenco dei parametri (INDTA) su IFS */
035300000000  ChgVar &CpyFromDTA ('/QSYS.LIB/QTEMP.LIB/' *CAT +
035400000000                      %sst(&SpcINDTA 1 10) *CAT '.USRSPC')
035500000000  ChgVar &CpyToDTA (&JobPath *TCAT %sst(&SpcINDTA 1 10))
035600000000  CPY OBJ(&CpyFromDTA) +
035700000000      TOOBJ(&CpyToDTA) +
035800000000      TOCCSID(819) DTAFMT(*TEXT) REPLACE(*YES)
035900000000
036000000000  ChgVar &NumCopia 1
036100000000
036200000000  /*?-------------------------------------------------------------- */
036300000000  /*?Elaborazione PDF                                               */
036400000000  /*?-------------------------------------------------------------- */
036500000000
036600000000  /*?Impostazione Server */
036700000000  If (&Server = S) ChgVar &JServer *YES
036800000000  Else ChgVar &JServer *NO
036900000000
037000000000  /*?Aggiunge le info */
037100000000  If (&Debug = S) ChgVar &oDebug *YES
037200000000  Else ChgVar &oDebug *NO
037300000000
037400000000ADDINFO:
037500000000  ChgVar &JError '0'
037600000000  ADDINFPDF  SPCINPDF(&JobPath *TCAT %sst(&SpcINPDF 1 10)) +
037700000000             SPCINDTA(&JobPath *TCAT %sst(&SpcINDTA 1 10)) +
037800000000             SPCOUPDF(&JobPath *TCAT %sst(&SpcOUPDF 1 10)) +
037900000000             TMPPATH(&JobPath) +
038000000000             JSERVER(&JServer) +
038100000000             JDEBUG(&oDEBUG)
038200000000  MonMsg CPF0000 Exec(Chgvar &JError '1')
038300000000  If (&Server = N) Do
038400000000    If (&Jerror) +
038500000000      SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
038600000000                 MsgDta('ADDINF    FAILED') +
038700000000                 ToPgmQ(*SAME) MsgType(*COMP)
038800000000    Else +
038900000000      SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
039000000000                 MsgDta('ADDINF    SUCCESS') +
039100000000                 ToPgmQ(*SAME) MsgType(*COMP)
039200000000  EndDo
039300000000  /* In caso di errore ritenta ...*/
039400000000  RcvMsg MsgQ(*PGMQ) MsgType(*LAST) Msg(&Msg)
039500000000  If (%sst(&Msg 11 7) *NE SUCCESS) Do
039600000000    /* messaggio video */
039700000000    If (*not &FlgMsg) Do
039800000000      RtvJoba TYPE(&Type)
039900000000      If (&Type = '1') Do
040000000000        ChgVar &ErrMsg ('>>>> ATTENZIONE <<<<< ' +
040100000000            *BCAT 'Documenti PDF mancanti nella stampa.' +
040200000000            *BCAT 'Premere INVIO per proseguire.')
040300000000        ChgVar &ErrMsgL 256
040400000000        Call QUILNGTX (&ErrMsg &ErrMsgL ' ' ' ' &ErrLength)
040500000000      EndDo
040600000000      ChgVar &FlgMsg '1'
040700000000    EndDo
040800000000    /* Imposta la stampa di debug ...*/
040900000000    If (*not &Error) Do
041000000000      ChgVar &oDebug *PRT
041100000000      ChgVar &Error '1'
041200000000      Goto ADDINFO
041300000000    EndDo
041400000000    ChgVar &LogData &KLIST
041500000000    ChgVar &LogMsg  ('Errore Fase ADDINFO')
041600000000    Call PDFLOGR ('ELENCO' &LogData '30' &LogMsg &TotPdf &TotNoPdf)
041700000000    MonMsg CPF0000
041800000000  EndDo
041900000000
042000000000  /*?Copia l'elenco dei PDF Output (OUPDF) da IFS in USRSPC */
042100000000  ChgVar &CpyFromPDF (&JobPath *TCAT %sst(&SpcOUPDF 1 10))
042200000000  ChgVar &CpyToPDF ('/QSYS.LIB/QTEMP.LIB/' *CAT +
042300000000                      %sst(&SpcOUPDF 1 10) *CAT '.USRSPC')
042400000000  CPY OBJ(&CpyFromPDF) +
042500000000      TOOBJ(&CpyToPDF) +
042600000000      TOCCSID(280) DTAFMT(*TEXT) REPLACE(*YES)
042700000000
042800000000  RtvJoba USER(&UserName)
042900000000  GRTOBJAUT  OBJ(QTEMP/%sst(&SpcOUPDF 1 10)) OBJTYPE(*USRSPC) +
043000000000             USER(&UserName) AUT(*ALL)
043100000000
043200000000  /*?Conta i Pdf generati */
043300000000  ChgVar &CurPdf 0
043400000000  ChgVar &OUPDFOff 1
043500000000  ChgVar &OUPDFLen 63
043600000000  DoUntil (&PdfPath *LE ' ')
043700000000    ChgVar &CurPdf (&CurPdf + 1)
043800000000    ChgVar &OUPDFOff (&CurPdf * 63)
043900000000    ChgVar &OUPDFOff (&OUPDFOff - 62)
044000000000    Call QUSRTVUS (&SpcOUPDF &OUPDFOff &OUPDFLen &PdfPath)
044100000000    MonMsg CPF3C06 Exec(ChgVar &PdfPath ' ')
044200000000  EndDo
044300000000  ChgVar &TotPdf (&CurPdf - 1)
044400000000
044500000000  /*?Imposta la risoluzione standard */
044600000000  ChgVar &pHires ' '
044700000000  ChgVar &Resolution &LoRes
044800000000
044900000000  /*?Composizione PDF */
045000000000  If (&TotPdf *GT 1) Do
045100000000
045200000000  /*?Crea 1 spazio per concatenamento a blocchi */
045300000000  ChgVar &SpcCOPDF ('C' *CAT &JobNbr *TCAT 'PDFQTEMP')
045400000000  ChgVar &SpcSize 22000
045500000000  Call QUSCRTUS +
045600000000    (&SpcCOPDF ' ' &SpcSize x'40' *ALL +
045700000000     'PDF per CONCATPDF' *yes X'00000000')
045800000000  ChgVar &CurPdf 0
045900000000
046000000000  /*?Carica i PDF a blocchi --------------------------------------- */
046100000000  BLOCCO:
046200000000    SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
046300000000               MSGDTA('Composizione PDF') +
046400000000               TOPGMQ(*EXT) MSGTYPE(*STATUS)
046500000000    ChgVar &BlkPdf  0
046600000000    ChgVar &CurSize 0
046700000000    ChgVar &INPDF ' '
046800000000
046900000000    /*?Carica un blocco fino al num.max o alla size max */
047000000000    DoWhile ((&BlkPdf < &MaxPdf) *AND +
047100000000             (&CurPdf < &TotPdf) *AND +
047200000000             (&CurSize < &MaxSizeKB))
047300000000      ChgVar &BlkPdf (&BlkPdf + 1)
047400000000      ChgVar &CurPdf (&CurPdf + 1)
047500000000      /*?Legge un PDF alla volta dall'elenco */
047600000000      ChgVar &SpcOff (&CurPdf * 63)
047700000000      ChgVar &SpcOff (&SpcOff - 62)
047800000000      ChgVar &SpcLen 63
047900000000      Call QUSRTVUS (&SpcOUPDF &SpcOff &SpcLen &PdfPath)
048000000000      /*?Interruzione per cambio risoluzione */
048100000000      ChgVar &cHiRes %sst(&PdfPath 62 1)
048200000000      If (&cHiRes *NE &pHiRes) Do
048300000000        If (&pHires *EQ 'H') ChgVar &Resolution &HiRes
048400000000        Else ChgVar &Resolution &LoRes
048500000000        ChgVar &pHiRes &cHiRes
048600000000        ChgVar &BlkPdf (&BlkPdf - 1)
048700000000        ChgVar &CurPdf (&CurPdf - 1)
048800000000        /*?Primo pdf del blocco */
048900000000        If (&BlkPdf = 0) Do
049000000000           If (&cHires *EQ 'H') ChgVar &Resolution &HiRes
049100000000           Else ChgVar &Resolution &LoRes
049200000000           Goto BLOCCO
049300000000        EndDo
049400000000        Else Leave                      /* Chiude elenco */
049500000000      EndDo
049600000000      ChgVar %sst(&PdfPath 62 1) ' '  /* toglie l'indicatore HiRes */
049700000000
049800000000      ChgVar &SpcOff (&BlkPdf * 63)
049900000000      ChgVar &SpcOff (&SpcOff - 62)
050000000000      ChgVar %sst(&InPDF &SpcOff 63) &PdfPath
050100000000      ChgVar %sst(&PdfPath 63 1) ' '  /* toglie il CR */
050200000000      ChgVar &TstPath (&PdfPath *TCAT &Null)
050300000000      CallPrc 'stat' Parm(&TstPath &FileInfo) RtnVal(&Rc)
050400000000      If (&Rc = 0) Do
050500000000        ChgVar &PdfSize (&ObjSize / 1024)
050600000000        ChgVar &CurSize (&CurSize + &PdfSize)
050700000000      EndDo
050800000000    EndDo
050900000000
051000000000    /* se c'è solo un Pdf in elenco salta il CONCAT */
051100000000    If (&BlkPdf = 1) Do
051200000000      ChgVar &SpcOff (&CurPdf * 63)
051300000000      ChgVar &SpcOff (&SpcOff - 62)
051400000000      Call QUSRTVUS (&SpcOUPDF &SpcOff &SpcLen &PdfPath)
051500000000      ChgVar %sst(&PdfPath 62 1) ' '  /* toglie l'indicatore HiRes */
051600000000      ChgVar %sst(&PdfPath 63 1) ' '  /* toglie il CR */
051700000000      ChgVar &OutPdf &PdfPath
051800000000      Goto PDF2PCL
051900000000    EndDo
052000000000
052100000000    ChgVar &OutPDF (&JobPath *TCAT 'Elenco.pdf')
052200000000
052300000000    If (&Debug = S) ChgVar &oDebug *YES
052400000000    Else ChgVar &oDebug *NO
052500000000  /* Copia l'elenco nello User Space */
052600000000  ChgVar &SpcOff 1
052700000000  ChgVar &INPDFLen 22000
052800000000  Call QUSCHGUS (&SpcCOPDF &SpcOff &INPDFLen &INPDF '0')
052900000000  /* Copia l'elenco dallo User Space in IFS */
053000000000  ChgVar &CpyFromPDF ('/QSYS.LIB/QTEMP.LIB/' *CAT +
053100000000                      %sst(&SpcCOPDF 1 10) *CAT '.USRSPC')
053200000000  ChgVar &CpyToPDF (&JobPath *TCAT %sst(&SpcCOPDF 1 10))
053300000000  CPY OBJ(&CpyFromPDF) +
053400000000      TOOBJ(&CpyToPDF) +
053500000000      TOCCSID(819) DTAFMT(*TEXT) REPLACE(*YES)
053600000000
053700000000  ChgVar &Error '0'
053800000000  CONCAT:
053900000000    ChgVar &JError '0'
054000000000    CONCAEPDF +
054100000000      INPDF(&JobPath *TCAT %sst(&SpcCOPDF 1 10)) +
054200000000      OUTPDF(&OutPDF) +
054300000000      JSERVER(&JServer) +
054400000000      JDEBUG(&oDebug)
054500000000    MonMsg CPF0000 Exec(Chgvar &JError '1')
054600000000    If (&Server = N) Do
054700000000      If (&Jerror) +
054800000000        SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
054900000000                   MsgDta('CONCAT    FAILED') +
055000000000                   ToPgmQ(*SAME) MsgType(*COMP)
055100000000      Else +
055200000000        SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
055300000000                   MsgDta('CONCAT    SUCCESS') +
055400000000                   ToPgmQ(*SAME) MsgType(*COMP)
055500000000    EndDo
055600000000    RcvMsg MsgQ(*PGMQ) MsgType(*LAST) Msg(&Msg)
055700000000    If (%sst(&Msg 11 7) *NE SUCCESS) Do
055800000000    /* messaggio video */
055900000000      If (*not &FlgMsg) Do
056000000000        RtvJoba TYPE(&Type)
056100000000        If (&Type = '1') Do
056200000000          ChgVar &ErrMsg ('>>>> ATTENZIONE <<<<< ' +
056300000000              *BCAT 'Documenti PDF mancanti nella stampa.' +
056400000000              *BCAT 'Premere INVIO per proseguire.')
056500000000          ChgVar &ErrMsgL 256
056600000000          Call QUILNGTX (&ErrMsg &ErrMsgL ' ' ' ' &ErrLength)
056700000000        EndDo
056800000000        ChgVar &FlgMsg '1'
056900000000      EndDo
057000000000      If (*not &Error) Do
057100000000        ChgVar &oDebug *PRT
057200000000        ChgVar &Error '1'
057300000000        Goto CONCAT
057400000000      EndDo
057500000000      ChgVar &LogData &KLIST
057600000000      ChgVar &LogMsg  ('Errore Fase CONCAT')
057700000000      Call PDFLOGR ('ELENCO' &LogData '30' &LogMsg &TotPdf &TotNoPdf)
057800000000      MonMsg CPF0000
057900000000    EndDo
058000000000  EndDo
058100000000
058200000000  /*?Solo un PDF in distinta  */
058300000000  Else Do
058400000000    ChgVar &SpcOff 1
058500000000    ChgVar &SpcLen 63
058600000000    ChgVar &CurPdf 1
058700000000    Call QUSRTVUS (&SpcOUPDF &SpcOff &SpcLen &PdfPath)
058800000000    ChgVar &OutPdf %sst(&PdfPath 1 61)
058900000000    If (%sst(&PdfPath 62 1) = 'H') ChgVar &Resolution &HiRes
059000000000    Else ChgVar &Resolution &LoRes
059100000000  EndDo
059200000000
059300000000  /*?Conversione PDF in PCL  */
059400000000PDF2PCL:
059500000000  SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
059600000000               MSGDTA('Conversione PDF in PCL') +
059700000000               TOPGMQ(*EXT)  MSGTYPE(*STATUS)
059800000000  ChgVar &OutPCL (&JobPath *TCAT 'Elenco.pcl')
059900000000  If (&Debug = S) ChgVar &oDebug *
060000000000  Else ChgVar &oDebug *NONE
060100000000  If (&Debug = S) +
060200000000    PDF2PCL PDF(&OutPDF) PCL(&OutPCL) DEVICE(&Device) +
060300000000            RESOLUTION(&Resolution) OUTPUT(*PRINT)
060400000000  Else +
060500000000    PDF2PCL PDF(&OutPDF) PCL(&OutPCL) DEVICE(&Device) +
060600000000            RESOLUTION(&Resolution) OUTPUT(*NONE)
060700000000
060800000000  /*?Converte PCL in SPLF */
060900000000  ChgVar &Retry 1
061000000000  DoUntil (&Retry = 20)
061100000000    ChgVar &Error '0'
061200000000    SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
061300000000               MSGDTA('Conversione PCL in SPLF') +
061400000000               TOPGMQ(*EXT)  MSGTYPE(*STATUS)
061500000000    PCL2SPLF PCL(&OutPCL) OUTQ(*LIBL/&PRINTER) USRDTA(PDF) +
061600000000             SPLFORMTYP(&FORMTYPE) SPLDRAWER(&DRAWER) +
061700000000             HOLD(*NO) +
061800000000             SPLFNAME(&SplfName) JOBNUMBER(&JobNumber) +
061900000000             USERNAME(&UserName) JOBNAME(&JobName) +
062000000000             SPLFNUMBER(&SplfNumber)
062100000000    MonMsg CPE3025 EXEC(ChgVar &Error '1')
062200000000    If (*not &Error) Leave
062300000000    ChgVar &aRetry &Retry
062400000000    SNDPGMMSG  MSGID(CPF9897) MSGF(QCPFMSG) +
062500000000               MSGDTA('Attesa completamento PCL -' *CAT &aRetry) +
062600000000               TOPGMQ(*EXT)  MSGTYPE(*STATUS)
062700000000    DLYJOB 1
062800000000    ChgVar &Retry (&Retry + 1)
062900000000  EndDo
063000000000  /*?... se la conversione non è riuscita invia un messaggio */
063100000000  If (&Retry = 20) Do
063200000000    ChgVar &ErrMsg ('Conversione PCL non riuscita')
063300000000    ChgVar &ErrMsgL 256
063400000000    RtvJoba TYPE(&Type)
063500000000    /*?... lavoro interattivo */
063600000000    If (&Type = '1') Do
063700000000      Call QUILNGTX (&ErrMsg &ErrMsgL ' ' ' ' &ErrLength)
063800000000    EndDo
063900000000    /*?... lavoro batch */
064000000000    Else Do
064100000000      ChgVar &Length 300
064200000000      ChgVar &ErrLength 0
064300000000      Call QUSRJOBI (&JOBI0400 &Length JOBI0400 * ' ' &ErrLength)
064400000000      ChgVar &SbmJob %sst(&JOBI0400 254 10)
064500000000      If (&SbmJob > ' ') +
064600000000        SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
064700000000                   MsgDta(&ErrMsg) +
064800000000                   ToMsgq(&SbmJob) MsgType(*COMP)
064900000000    Enddo
065000000000  EndDo
065100000000
065200000000  /*?Cancella PDF e PCL */
065300000000  If (&Debug *NE S) Do
065400000000   del &OutPDF
065500000000   MonMsg CPF0000
065600000000   del &OutPCL
065700000000   MonMsg CPF0000
065800000000  EndDo
065900000000
066000000000  /*?Ripristina la risoluzione standard */
066100000000  ChgVar &pHires ' '
066200000000  ChgVar &Resolution &LoRes
066300000000
066400000000  /*?Prepara un altro blocco di PDF ------------------------------- */
066500000000  If (&CurPdf < &TotPdf) GOTO BLOCCO
066600000000
066700000000  /*?Cancella PDF e PCL  */
066800000000  If (&Debug *NE S) Do
066900000000   /* Cancella i PDF temporanei */
067000000000   If (&TotPdf *GT 1) Do
067100000000     Dowhile (&TotPdf > 0)
067200000000       /* Legge un PDF alla volta dall'elenco */
067300000000       ChgVar &SpcOff (&TotPdf * 63)
067400000000       ChgVar &SpcOff (&SpcOff - 62)
067500000000       Call QUSRTVUS (&SpcOUPDF &SpcOff &SpcLen &PdfPath)
067600000000       ChgVar %sst(&PdfPath 62 1) ' '  /* toglie l'indicatore HiRes */
067700000000       ChgVar %sst(&PdfPath 63 1) ' '  /* toglie il CR */
067800000000       del &PdfPath
067900000000       MonMsg CPF0000
068000000000       ChgVar &TotPdf (&TotPdf - 1)
068100000000     EndDo
068200000000   EndDo
068300000000   /*?Cancella gli elenchi */
068400000000   ChgVar &PdfPath (&JobPath *TCAT %sst(&SpcINPDF 1 10))
068500000000   del &PdfPath
068600000000   MonMsg CPF0000
068700000000   ChgVar &PdfPath (&JobPath *TCAT %sst(&SpcINDTA 1 10))
068800000000   del &PdfPath
068900000000   MonMsg CPF0000
069000000000   ChgVar &PdfPath (&JobPath *TCAT %sst(&SpcOUPDF 1 10))
069100000000   del &PdfPath
069200000000   MonMsg CPF0000
069300000000   If (&SpcCOPDF *NE ' ') Do
069400000000     ChgVar &PdfPath (&JobPath *TCAT %sst(&SpcCOPDF 1 10))
069500000000     del &PdfPath
069600000000     MonMsg CPF0000
069700000000   EndDo
069800000000   /*?Cancella gli UserSpace */
069900000000   Call QUSDLTUS (&SpcINPDF x'0000000000000000')
070000000000   Call QUSDLTUS (&SpcINDTA x'0000000000000000')
070100000000   Call QUSDLTUS (&SpcOUPDF x'0000000000000000')
070200000000   If (&SpcCOPDF *NE ' ') +
070300000000     Call QUSDLTUS (&SpcCOPDF x'0000000000000000')
070400000000  EndDo
070500000000
070600000000  /*?Elimina il path di lavoro */
070700000000  If (&Debug *NE S) Do
070800000000    RD &JobPath
070900000000    MonMsg CPFA0AC Exec(Do)
071000000000      chgvar &Cmd ('rm -rf' *BCAT &JobPath)
071100000000      QSH CMD(&Cmd)
071200000000      MonMsg Cpf0000
071300000000    EndDo
071400000000  EndDo
071500000000
071600000000  /*?Log fine */
071700000000  ChgVar &LogData &KLIST
071800000000  ChgVar &LogMsg  ('Fine Stampa PDF')
071900000000  Call PDFLOGR ('ELENCO' &LogData '00' &LogMsg +
072000000000                &TotPdf &TotNoPdf *CLOSE)
072100000000  MonMsg CPF0000
072200000000  SndPgmMsg  MsgId(CPF9898) Msgf(QCPFMSG) +
072300000000             MsgDta('Stampa elenco' *BCAT &KLIST *BCAT 'completata') +
072400000000             MsgType(*COMP)
072500000000
072600000000FINE:
072700000000
072800000000Return
072900000000
073000000000ERROR:
073100000000  /*?Log ERRORE x-------------------------------------------------- */
073200000000  If (&FlgLog) Return
073300000000  ChgVar &FlgLog '1'
073400000000  ChgVar &TotPdf 0
073500000000  ChgVar &TotNoPdf 0
073600000000  ChgVar &LogData &KLIST
073700000000  ChgVar &LogMsg  ('ERRORE Stampa PDF')
073800000000  Call PDFLOGR ('ELENCO' &LogData '30' &LogMsg +
073900000000                &TotPdf &TotNoPdf *CLOSE)
074000000000  MonMsg CPF0000
074100000000
074200000000  DspJobLog Output(*Print)
074300000000  RclRsc
074400000000
074500000000ENDPGM
