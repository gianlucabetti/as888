000100130114/*PARMS DFTACTGRP(*NO) ACTGRP(*CALLER)                                        */
000200990907/*PARMS                                                                       */
000300990907/*PARMS                                                                       */
000400990907/*PARMS                                                                       */
000500990907/*PARMS                                                                       */
000600080526             PGM        PARM(&TIVLR &ESITO &LIT &FIR &FID &MBRFIT &TOTRCD)
000700990908
000800990920             DCLF       FILE(TIVLR00F)
000900991110             DCL        VAR(&TIVLR) TYPE(*CHAR) LEN(304)
001000990908             DCL        VAR(&ESITO) TYPE(*CHAR) LEN(1)
001100021118             DCL        VAR(&ESITOSTOR) TYPE(*CHAR) LEN(1)
001200990920             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
001300990915             DCL        VAR(&USRDTA) TYPE(*CHAR) LEN(10)
001400001214             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)
001500000724             DCL        VAR(&LIT) TYPE(*CHAR) LEN(10)
001600000724             DCL        VAR(&FIR) TYPE(*CHAR) LEN(10)
001700021118             DCL        VAR(&FID) TYPE(*CHAR) LEN(10)
001800080421             DCL        VAR(&LLIST) TYPE(*CHAR) LEN(2750)
001900080421             DCL        VAR(&CMD) TYPE(*CHAR) LEN(3000)
002000080421             DCL        VAR(&NN) TYPE(*DEC) LEN(15 5) VALUE(3000)
002100021118             DCL        VAR(&MBRDATA) TYPE(*CHAR) LEN(13)
002200051013             DCL        VAR(&MBRFIT) TYPE(*CHAR) LEN(1)
002300080526             DCL        VAR(&TOTRCD) TYPE(*DEC) LEN(7 0) VALUE(0)
002400170607             DCL        VAR(&OPZ)     TYPE(*CHAR) LEN(3) VALUE('STR')
002500170607             DCL        VAR(&ESI)     TYPE(*CHAR) LEN(1) VALUE(' ')
002600990921
002700990921             CHGVAR     VAR(&VLRKSC) VALUE(%SST(&TIVLR 1 8))
002800991110             CHGVAR     VAR(&VLRFIS) VALUE(%SST(&TIVLR 130 10))
002900991110             CHGVAR     VAR(&VLRHDL) VALUE(%SST(&TIVLR 140 10))
003000991110             CHGVAR     VAR(&VLRPGT) VALUE(%SST(&TIVLR 150 10))
003100991110             CHGVAR     VAR(&VLRFOU) VALUE(%SST(&TIVLR 210 10))
003200000710
003300010726
003400010726/* Come primissima cosa salvo la lista librerie iniziale della JOBD -*/
003500010726             RTVJOBA    USRLIBL(&LLIST)
003600010726             MONMSG     MSGID(CPF0000)
003700990920
003800990920/* Compongo il nome del membro --------------------------------------*/
003900990920             CHGVAR     VAR(&MBR) VALUE(&VLRHDL)
004000990920             CHGVAR     VAR(%SST(&MBR 1 1)) VALUE('M')
004100990908
004200051013/* Se richiesto aggiungo il membro al file di arrivo -----------------------------*/
004300051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
004400001013             CHKOBJ     OBJ(&VLRFOU) OBJTYPE(*FILE) MBR(&MBR)
004500070820             MONMSG     MSGID(CPF5815 CPF9815) EXEC(DO)
004600000717             ADDPFM     FILE(&VLRFOU) MBR(&MBR) TEXT(&VLRKSC)
004700001013             MONMSG     MSGID(CPF0000) EXEC(DO)
004800001013             DLYJOB     DLY(300)
004900001013             ADDPFM     FILE(&VLRFOU) MBR(&MBR) TEXT(&VLRKSC)
005000001013             ENDDO
005100001013             ENDDO
005200051013             ENDDO
005300001013
005400990910/* Alloco il membro nei due file ------------------------------------*/
005500001214/* ... se non ci riesco preferisco un bel CPF -----------------------*/
005600051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
005700000717             ALCOBJ     OBJ((&VLRFOU *FILE *EXCL &MBR))
005800140404/* Se errore gestisco 1 attesa + 1 ritentativo ----------------------*/
005900140404             MONMSG     MSGID(CPF0000) EXEC(DO)
006000140404             DLYJOB     DLY(120)
006100140404             ALCOBJ     OBJ((&VLRFOU *FILE *EXCL &MBR))
006200140404             ENDDO
006300051013             ENDDO
006400140404
006500021119             ALCOBJ     OBJ((&FID *FILE *EXCL &MBR))
006600140404/* Se errore gestisco 1 attesa + 1 ritentativo ----------------------*/
006700140404             MONMSG     MSGID(CPF0000) EXEC(DO)
006800140404             DLYJOB     DLY(120)
006900140404             ALCOBJ     OBJ((&FID *FILE *EXCL &MBR))
007000140404             ENDDO
007100140404
007200170607
007300990910
007400170607/* Punto al membro --------------------------------------------------*/
007500021119             OVRDBF     FILE(&FID) MBR(&MBR) OVRSCOPE(*JOB)
007600051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
007700170607
007800170607/* Verifico necessità di sostituire a 1 il "Force-RCcord-RATIO" -----*/
007900170607             CALL       PGM(TIS711C1) PARM(&OPZ &VLRFOU &ESI)
008000170607             IF         COND(&ESI *EQ 'N') THEN(DO)
008100170504             OVRDBF     FILE(&VLRFOU) MBR(&MBR) FRCRATIO(1) +
008200170505                          OVRSCOPE(*JOB) SHARE(*YES)
008300051013             ENDDO
008400170607             ELSE       CMD(DO)
008500170607             OVRDBF     FILE(&VLRFOU) MBR(&MBR) OVRSCOPE(*JOB) SHARE(*YES)
008600170607             ENDDO
008700170607             ENDDO
008800990915
008900990910/* Chiamo il traduttore ---------------------------------------------*/
009000990916             CHGVAR     VAR(&ESITO) VALUE(' ')
009100010618             CALL       PGM(&VLRPGT) PARM(&TIVLR &ESITO &LIT &FIR)
009200990908             MONMSG     MSGID(CPF0000) EXEC(DO)
009300010618             IF         COND(&ESITO *EQ ' ') THEN(DO)
009400010618             CHGVAR     VAR(&ESITO) VALUE('5')
009500010618             ENDDO
009600990908             ENDDO
009700010328
009800010726/* In uscita di ogni traduttore - */
009900010726/* per sicurezza re-imposto la corretta LIBL (quella della JOBD TIS700C) -*/
010000020506             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT &LLIST +
010100020506                          *CAT ')')
010200020506             CALL       PGM(QCMDEXC) PARM(&CMD &NN)
010300020506             MONMSG     MSGID(CPF0000)
010400010328
010500010207
010600010525/* Verifico esito ed eseguo eventuali operazioni di "sfleggatura" membro input (TIVIN) -----*/
010700010529             IF         COND(&ESITO *EQ ' ') THEN(DO)
010800010525             CALL       PGM(TIS710R)
010900010525             ENDDO
011000010529             IF         COND(&ESITO *EQ '9') THEN(DO)
011100010529             CHGVAR     VAR(&ESITO) VALUE(' ')
011200010529             ENDDO
011300021105
011400021105/* Registro nel log l'esito della traduzione ------------------------*/
011500021105             IF         COND(&ESITO = '0') THEN(DO)
011600021105             SNDPGMMSG  MSG('INFO: la traduzione del file' *BCAT +
011700021119                          &FID *TCAT '(' *TCAT &MBR *TCAT ')' +
011800021105                          *BCAT 'in' *BCAT &VLRFOU *BCAT 'è stata +
011900021105                          eseguita con successo.')
012000021105             ENDDO
012100021105             IF         COND(&ESITO > '0') THEN(DO)
012200021105             SNDPGMMSG  MSG('ERRORE: durante la traduzione del file' +
012300021119                          *BCAT &FID *TCAT '(' *TCAT &MBR *TCAT +
012400021105                          ')' *BCAT 'in' *BCAT &VLRFOU *BCAT 'si +
012500021105                          sono verificati degli errori.')
012600021105             ENDDO
012700021118
012800021204 /* Se esito ok (o cmq è avvenuta correttamente una traduzione) */
012900021204             IF         COND(&ESITO='0' *OR &ESITO='1' *OR &ESITO='3' *OR &ESITO='6') THEN(DO)
013000021118 /* Inizializzo il flag che mi indica: */
013100021118 /* 0 = da NON storicizzare            */
013200021118 /* 1 = storicizzazione OK             */
013300021118 /* 2 = storicizzazione NON OK         */
013400021118             CHGVAR     VAR(&ESITOSTOR) VALUE('0')
013500021118 /* Se il file di INPUT ed il file STORICO sono differenti => procedo con la storicizzazione */
013600021118             IF         COND(&VLRFIS *NE &FID) THEN(DO)
013700021118 /* Reperisco la data di creazione del membro da storicizzare */
013800021118             RTVMBRD    FILE(&FID) MBR(&MBR) CRTDATE(&MBRDATA)
013900021118 /* Chiamo il programma di stroricizzazione */
014000080526             CALL       PGM(TIS712R) PARM(&TIVLR &ESITOSTOR &MBR &MBRDATA &TOTRCD)
014100021118             ENDDO
014200021118             ENDDO
014300010525
014400010525/* Cancello le override sui file trattati ---------------------------*/
014500051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
014600010530             DLTOVR     FILE(&VLRFOU) LVL(*JOB)
014700010530             MONMSG     MSGID(CPF0000)
014800051013             ENDDO
014900021119             DLTOVR     FILE(&FID) LVL(*JOB)
015000010530             MONMSG     MSGID(CPF0000)
015100010525
015200010525/* Disalloco i membri -----------------------------------------------*/
015300051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
015400010525             DLCOBJ     OBJ((&VLRFOU *FILE *EXCL &MBR))
015500051013             ENDDO
015600021119             DLCOBJ     OBJ((&FID *FILE *EXCL &MBR))
015700010529
015800010529/* Elimino il membro del file (di output) tradotto/inviato ----------*/
015900051013             IF         COND(&MBRFIT *NE 'N') THEN(DO)
016000010529             RMVM       FILE(&VLRFOU) MBR(&MBR)
016100070820             MONMSG     MSGID(CPF5815 CPF7310 CPF9815) EXEC(DO)
016200070820             CHKOBJ     OBJ(&VLRFOU) OBJTYPE(*FILE) MBR(&MBR)
016300070820             MONMSG     MSGID(CPF5815 CPF9815)
016400070820             ENDDO
016500051013             ENDDO
016600021118
016700021118/* Se occore elimino il membro del file (di input) tradotto/inviato -*/
016800021118             IF         COND(&ESITOSTOR = '1') THEN(DO)
016900021118             RMVM       FILE(&FID) MBR(&MBR)
017000070820             MONMSG     MSGID(CPF5815 CPF7310 CPF9815) EXEC(DO)
017100070820             CHKOBJ     OBJ(&FID) OBJTYPE(*FILE) MBR(&MBR)
017200070820             MONMSG     MSGID(CPF5815 CPF9815)
017300070820             ENDDO
017400021118             ENDDO
017500021118
017600021118/* Se errore in storicizzazione -------------------------------------*/
017700021118             IF         COND(&ESITOSTOR = '2') THEN(DO)
017800021118             CHGVAR     VAR(&ESITO) VALUE('7')
017900021118             ENDDO
018000010615
018100010615/* Se in errore verifico esistenza traduttore -----------------------*/
018200010615             IF         COND(&ESITO > '0') THEN(DO)
018300010615             CHKOBJ     OBJ(&VLRPGT) OBJTYPE(*PGM)
018400010615             MONMSG     MSGID(CPF0000) EXEC(DO)
018500010615             CHGVAR     VAR(&ESITO) VALUE('4')
018600010615             ENDDO
018700010615             ENDDO
018800990908
018900990907             ENDPGM
