000100890918     A*--------------------------------------------------------------*
000200090318     A*  TABELLA  CAUSALI CONTATTO POTENZIALI / CLIENTI
000300890918     A*--------------------------------------------------------------*
000400890918     A                                      REF(CNDIZION)
000500090318     A          R D§CCO                     TEXT('CAUSALI CONTATTO')
000600090318     A            §CCODES       35A         TEXT('DESCRIZIONE')
000700090422     A            §CCOGIO        3S 0       TEXT('GIORNI PROSSIMO CONTATTO-
000800090511     A                                       PROPOSTI')
000900100924     A            §CCOUPM        1A         TEXT('PROSSIMO CONTATTO =+- GG §CCO-
001000100924     A                                      GIO')
001100130104     A            §CCOSTP       20A         TEXT('LIBERO')
001200130104     A            §CCOSTC       20A         TEXT('LIBERO')
001300090609     A            §CCOORD        2A         TEXT('TIPOLOGIA/ORDINAMENTO/PRIORI-
001400090609     A                                      TA'' CAUSALE')
001500090629     A            §CCOTAT        1A         TEXT('TIPO CONTATTO FUTURO')
001600090702     A            §CCOUTI        1A         TEXT('FLAG UTILIZZO DA UTENTE;N=NO')
001700091112     A            §CCOAPT        1A         TEXT('S=APRE SEMPRE TRATTATIVA')
001800100305     A            §CCOTRA        1A         TEXT('S=LEGATA TRATTATIVA; N=NON -
001900100305                                            DEVE ESSERCI TRATTATIVA')
002000091130     A            §CCOESI        1A         TEXT('ESITO ASSOCIATO')
002100100528     A            §CCOSTA        1A         TEXT('S=DA STAMPARE IN STATISTI-
002200100528                                            CA;" "=DA NON STAMPARE')
002300091130     A            §CCOOPZ        5A         TEXT('MAX 5 OPZIONI INIBITE')
002400091218     A            §CCOEAF        2A         TEXT('CAUSALE DA IMPOSTARE AGLI -
002500091130                                            AFFIANCAMENTI')
002600101209     A            §CCONOT        1A         TEXT('S=INSERIMENTO NOTE OBBLIG.')
002700091210     A            §CCOESO        1A         TEXT('S=RICHIAMARE PGM ESITO OFFE-
002800091210                                            RTA')
002900091217     A            §CCOATT        1A         TEXT('TIPO ATTIVITÀ ASSOCIATA')
003000100218      * FLAG PER UTILIZZO DELLA CAUSALI COME ESITO DI UNA ATTIVITÀ ESEGUITA
003100100218      *
003200100219      * SCHIERA DI 10 TIPI ATTIVITA' ABILITATE ALL'UTILIZZO DI QUESTA
003300100219      * CAUSALE NELLA FASE DI ESECUZIONE ATTIVITÀ
003400100218      *
003500100219     A            §CCOTAA       10A         TEXT('10 TIPI ATTIVITA ABILITATE')
003600100219      * ALCUNI CAUSALI NON POSSONO ESSERE USATE SE LEGATE A TRATTATIVA CON
003700100219      * OFFERTE ANCORA DA ESITARE O ACCETTATE
003800100305     A            §CCOOFF        1A         TEXT('N=NON UTILIZZABILE SE PRESEN-
003900100219     A                                      TI OFFERTE')
004000100304      * FLAG CHE PILOTA LA CHIUSURA DELLA TRATTATIVA
004100100304     A            §CCOCHT        1A         TEXT('S=CHIUDE TRATTATIVA')
004200100304      * FLAG CHE PILOTA L'UTILIZZO DELLA CAUSALE SOLO CLIENTI
004300100304      * O SOLO POTENZIALI O TUTTI
004400100304     A            §CCOCOD        1A         TEXT('C=SOLO CODIFICATI;P=MAI CO-
004500100304     A                                      DIFICATI; =TUTTO')
004600100304      * FLAG CHE PILOTA LA RICHIESTA DELL'ESITO POSITIVO/NEGATIVO ATTIVITA'
004700100304      * IN ESECUZIONE
004800100304     A            §CCORES        1A         TEXT('S=RICHIESTA A VIDEO ESITO -
004900100304     A                                      POSITIVO / NEGATIVO')
005000100305      * FLAG CHE CONTROLLO L'ESEGUIBILITÀ DELL'ATTIVITA DA COMMERCIALE INTERNO
005100100305     A            §CCOCOI        1A         TEXT('N=NON ESEGUIBILE DA COMMERCIA-
005200100305     A                                      LE INTERNO;F=NON ESEGUIBILE MA FOR-
005300100305     A                                      ZABILE; =NO CONTROLLO')
005400101209      * 10 TIPI TRATTATIVA (DA TAB. "TTR")
005500101209     A            §CCOTPV       10A         TEXT('10 TIPI TRATTATIVA ABILITATI')
005600101209      * FLAG CHE PILOTA IL RICHIAMO AL PGM GESTIONE DATA CALDO
005700101209     A            §CCOHOT        1A         TEXT('S=RICHIAMARE PGM GESTIONE DAT-
005800101209     A                                      A DEL CALDO')
005900101209      * QUESTA CAUSALE PUO' ESSERE UTILIZZATA SOLO PER ESITARE LA SEGUENTE
006000101209      * CAUSALE
006100101209     A            §CCOCAE        2A         TEXT('CAUSALE PER LA QUALE SI PUÒ -
006200101209     A                                      UTILIZZARE')
006300101209      * FLAG CHE IMPEDISCE L'ANNULLAMENTO DELL'ATTIVITA'
006400101209     A            §CCOANN        1A         TEXT('S=IMPEDIRE ANNULLAMENTO ATTI-
006500101209     A                                      TÀ')
006600101209      * FLAG CHE PILOTA IL RICHIAMO DEL PGM CLIENTI DA BLOCCARE
006700101209     A            §CCOBLC        1A         TEXT('S=RICHIAMARE IL PGM CLIENTI -
006800101209     A                                      DA BLOCCARE')
006900101214      * FLAG CHE PILOTA IL RICHIAMO DEL PGM CONTROLLO CLIENTE PERSO
007000101214     A            §CCOCCP        1A         TEXT('S=RICHIAMARE IL PGM CONTROLLO-
007100101214     A                                       CLIENTE PERSO')
007200110211      * FLAG CHE PILOTA IL RICHIAMO DEL PGM STAMPA BLOCCO CLIENTI (TESTO 54)
007300110211     A            §CCOSTL        1A         TEXT('S=RICHIAMARE IL PGM STAMPA LE-
007400110211     A                                      TTERA BLOCCO CLIENTE')
007500110211      * FLAG CHE PILOTA IL RICHIAMO AL PGM (TNTA60R) X IMPOSTARE BLOCCO CLIENTE
007600110211     A            §CCOPGMBLC     1A         TEXT('S=RICHIAMARE IL PGM PER BLOCC-
007700110211     A                                      ARE IL CLIENTE')
007800110405      * CAUSALE GESTIONE DEL CLIENTE CHE NON SPEDISCE
007900110405     A            §CCOCNS        1A         TEXT('S=CAUSALE CLIENTE CHE NON SPE-
008000110405     A                                       DISCE')
008100110405      * QUESTA CAUSALE PUÒ ESSERE UTILIZZATA PER ESITARE UNA CAUSALE DI
008200110405      * UN CLIENTE CHE NON SPEDISCE ?????
008300110405     A            §CCOUCNS       1A         TEXT('S=QUESTA CAUSALE PUÒ ESSERE U-
008400110405     A                                      TILIZZATA PER CAUSALE CLIENTE CHE -
008500110405     A                                      NON SPEDISCE;N=NO;BLANKS=TUTTE')
008600110429      * SCHIERA DI 4 CATEGORIE POTENZIALI ABILITATE
008700110429      *
008800110429     A            §CCOCPO        4A         TEXT('4 CATEGORIE POTENZIALI ABILI-
008900110429     A                                      TATE')
009000120622      * FLAG CHE PILOTA L'AGGIORNAMENTO O MENO DELLA DATA PRIMO CONTATTO
009100120622      * SU TNCPO
009200120622     A            §CCODPC        1A         TEXT('N=NON AGGIORNA DATA PRIMO -
009300120622     A                                      CONTATTO SUL POTENZIALE')
